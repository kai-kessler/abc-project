@ <stdio.hdr>
@ "lexer.hdr"
@ "parser.hdr"
@ "gen.hdr"

fn parseExpr(): -> Expr
{
    local left: -> Expr = parseTerm();
    if (!left) {
        return nullptr;
    }
    while (token.kind == PLUS) {
        getToken();
        local right: -> Expr = parseTerm();
        if (!right) {
            return nullptr;
        }
        left = createBinaryExpr(EXPR_ADD, left, right);
    }
    return left;
}

fn parseTerm(): -> Expr
{
    local left: -> Expr = parseFactor();
    if (!left) {
        return nullptr;
    }
    while (token.kind == ASTERISK) {
        getToken();
        local right: -> Expr = parseFactor();
        if (!right) {
            return nullptr;
        }
        left = createBinaryExpr(EXPR_MUL, left, right);
    }
    return left;
}

fn parseFactor(): -> Expr
{
    if (token.kind == DECIMAL_LITERAL) {
        local expr: -> Expr = createIntegerExpr(token.val);
        getToken();
        return expr;
    } else if (token.kind == LPAREN) {
        getToken();
        local expr: -> Expr = parseExpr();
        if (!expr) {
            return nullptr;
        }
        if (token.kind != RPAREN) {
            return nullptr;
        }
        getToken();
        return expr;
    } else {
        return nullptr;
    }
}
